

class Avatar {
    // Location on the screen
    field int x, y;

    // The size of the square
    field int size;

    /** Constructs a new square with a given location and size. */
    constructor Avatar new(int Ax, int Ay) {
        let x = Ax;
        let y = Ay;
        let size = 16;

        do draw();

        return this;
    }

    /** Deallocates the object's memory. */
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    /** Draws the avatar on the screen. */
    method void draw() {
        var int location;  // a 16-bit address w.r.t the screen address 16384
        var int memAddress;
        let location = (32*y) + (x/16);
        let memAddress = 16384+location;
        // each memory addr stores 16-bit value, namely 16 pixels (only white and black)
        // for example, memAddr=16384 represents the first 16 pixels (x=0..15, y=0)
        // memAddr=16385 represents the next 16 pixels (x=16..31, y=0)
        // for the screen with width of 512 pixels, we have 512/16 = 32
        // namely, if we go to next line (y + 1) with the same x, we have to use (memAddr+32)
        do Memory.poke(memAddress+0, 4064);
        do Memory.poke(memAddress+32, 6192);  // 32*16 = 512
        do Memory.poke(memAddress+64, 4752);
        do Memory.poke(memAddress+96, 4112);
        do Memory.poke(memAddress+128, 2080);
        do Memory.poke(memAddress+160, 6192);
        do Memory.poke(memAddress+192, 8456);
        do Memory.poke(memAddress+224, 16644);
        do Memory.poke(memAddress+256, 19364);
        do Memory.poke(memAddress+288, 20500);
        do Memory.poke(memAddress+320, 12312);
        do Memory.poke(memAddress+352, 4112);
        do Memory.poke(memAddress+384, 4368);
        do Memory.poke(memAddress+416, 4752);
        do Memory.poke(memAddress+448, 16120);
        do Memory.poke(memAddress+480, 32508);
        return;
    }

    /** Erases the avatar from the screen. */
    method void erase() {
        var int location;  // a 16-bit address w.r.t the screen address 16384
        var int memAddress;
        let location = (32*y) + (x/16);
        let memAddress = 16384+location;
        do Memory.poke(memAddress+0, 0);
        do Memory.poke(memAddress+32, 0);
        do Memory.poke(memAddress+64, 0);
        do Memory.poke(memAddress+96, 0);
        do Memory.poke(memAddress+128, 0);
        do Memory.poke(memAddress+160, 0);
        do Memory.poke(memAddress+192, 0);
        do Memory.poke(memAddress+224, 0);
        do Memory.poke(memAddress+256, 0);
        do Memory.poke(memAddress+288, 0);
        do Memory.poke(memAddress+320, 0);
        do Memory.poke(memAddress+352, 0);
        do Memory.poke(memAddress+384, 0);
        do Memory.poke(memAddress+416, 0);
        do Memory.poke(memAddress+448, 0);
        do Memory.poke(memAddress+480, 0);
        return;
    }

    /** Moves up by 2 pixels. */
    method void moveUp() {
        if (y > 1) {
            do erase();
            let y = y - 2;
            do draw();
        }
        return;
    }

    /** Moves down by 2 pixels. */
    method void moveDown() {
        if ((y + size) < 254) {
            do erase();
            let y = y + 2;
            do draw();
        }
        return;
    }

    /** Moves left by 2 pixels. */
    method void moveLeft() {
        if (x > 1) {
            do erase();
            let x = x - 2;
            do draw();
        }
        return;
    }

    /** Moves right by 2 pixels. */
    method void moveRight() {
        if ((x + size) < 510) {
            do erase();
            let x = x + 2;
            do draw();
        }
        return;
    }
}