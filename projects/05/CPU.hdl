// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:
    // AI is A-instruction, CI is C-instruction
    DMux(in=true, sel=instruction[15], a=AI, b=CI);

    // A register
    // comp: the result of ALU can be used ahead
    And(a=CI, b=instruction[5], out=writeA);  // dest of A
    Or(a=AI, b=writeA, out=loadA);  // load when AI or writeA
    Mux16(a=instruction, b=comp, sel=writeA, out=inOrComp);
    ARegister(in=inOrComp, load=loadA, out=aout);

    // D
    And(a=CI, b=instruction[4], out=writeD);
    DRegister(in=comp, load=writeD, out=dout);

    // Based on Figure 4.3
    // if a=0, D is x input of ALU, A is y input
    // if a=1, D is x input of ALU, M is y input
    Mux16(a=aout, b=inM, sel=instruction[12], out=AorM);

    ALU(x=dout, y=AorM, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=comp, zr=zr, ng=ng);


    And(a=CI, b=instruction[3], out=writeM);
    Or16(a=false, b=comp, out=outM);
    
    // Credit: https://github.com/havivha/Nand2Tetris/blob/master/05/CPU.hdl#L61
    // how to output part of the pins
    Or16(a=false, b=aout, out[0..14]=addressM);

    // set PC
    // Figure 4.5
    And(a=zr, b=instruction[1], out=jeq);  // out is zero and jump on zero
    And(a=ng, b=instruction[2], out=jlt);
    Or(a=jeq, b=jlt, out=jle);

    Or(a=zr, b=ng, out=negORzero);
    Not(in=negORzero, out=pos);
    And(a=pos, b=instruction[0], out=jgt);

    // we have all the jump conditions so far
    Or(a=jle, b=jgt, out=jmpA);
    
    // credit: https://github.com/havivha/Nand2Tetris/blob/master/05/CPU.hdl#L73
    // Forgot this: only jump if CI
    And(a=CI, b=jmpA, out=PCload);

    Not(in=PCload, out=inc);
    PC(in=aout, load=PCload, inc=inc, reset=reset, out[0..14]=pc);

    

}