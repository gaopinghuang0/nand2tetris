// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Screen.jack

/**
 * Graphic screen library.
 */
class Screen {
    static Array screen;
    static boolean color;
    static boolean black;
    static boolean white;


    /** Initializes the Screen. */
    function void init() {
        let screen = 16384;
        let black = true;
        let white = false;
        let color = black;
        return;
    }

    /** Erases the whole screen. */
    function void clearScreen() {
        var int i;
        let i = 0;
        while (i < 8192) {
            let screen[i] = white;
            let i = i+1;
        }
        return;
    }

    /** Sets the color to be used in further draw commands
     *  where white = false, black = true. */
    function void setColor(boolean b) {
        let color = b;
        return;
    }

    /** Draws the (x, y) pixel. */
    function void drawPixel(int x, int y) {
        var int addr, mask;
        let addr = (32*y) + (x/16);
        // credit: https://github.com/havivha/Nand2Tetris/blob/master/12/Screen.jack
        // x % 16 --> x & 15
        let mask = Math.two_to_the(x & 15);

        if (color) {
            let screen[addr] = screen[addr] | mask;
        } else {
            let screen[addr] = screen[addr] & ~mask;
        }
        return;
    }

    /** Draws a line from (x1, y1) to (x2, y2). */
    function void drawLine(int x1, int y1, int x2, int y2) {
        if (x1 = x2) {
            do Screen.drawVerticalLine(x1, y1, y2);
        } else {
            if (y1 = y2) {
                do Screen.drawHorizontalLine(x1, x2, y1);
            } else {
                do Screen.drawDiagonalLine(x1, y1, x2, y2);
            }
        }
        return;
    }

    function void drawVerticalLine(int x, int y1, int y2) {
        var int temp;
        // make sure y1 <= y2
        if (y1 > y2) {
            let temp = y1;
            let y1 = y2;
            let y2 = temp;
        }

        while (~(y1 > y2)) {
            do Screen.drawPixel(x, y1);
            let y1 = y1 + 1;
        }

        return;
    }

    function void drawHorizontalLine(int x1, int x2, int y) {
        // credit: https://github.com/havivha/Nand2Tetris/blob/master/12/Screen.jack
        // optimization, draw 16 pixels at once by writing one memory address
        var int temp;
        var int start_addr, end_addr;
        var int x1mod16, x2mod16;
        // make sure x1 <= x2
        if (x1 > x2) {
            let temp = x1;
            let x1 = x2;
            let x2 = temp;
        }

        let x1mod16 = x1 & 15;
        let x2mod16 = x2 & 15;
        let start_addr = (y*32) + (x1/16);
        // x2mod16=0 <=> x2 % 16 = 0; mostly it's false, just add 0 to end_addr
        let end_addr = (y*32) + (x2/16) + (x2mod16=0);

        if (start_addr = end_addr) {  // line starts and ends within the same memory addr
            do Screen.drawShortHorizontalLine(x1, x2, y);
        } else {
            if (~(x1mod16 = 0)) {  // draw the short start of the line
                let start_addr = start_addr + 1;
                do Screen.drawShortHorizontalLine(x1, x1+16-x1mod16, y);
            }
            if (~(x2mod16 = 0)) {  // draw the short end of the line
                let end_addr = end_addr - 1;
                do Screen.drawShortHorizontalLine(x2-x2mod16, x2, y);
            }
            while (~(start_addr > end_addr)) { // draw the middle of the line
                let screen[start_addr] = color;
                let start_addr = start_addr + 1;
            }
        }

        return;
    }

    function void drawShortHorizontalLine(int x1, int x2, int y) {
        while (~(x1 > x2)) {
            do Screen.drawPixel(x1, y);
            let x1 = x1 + 1;
        }
        return;
    }

    function void drawDiagonalLine(int x1, int y1, int x2, int y2) {
        // see images/Screen.drawLine*.png
        var int temp;
        var int y_incr;
        var int dx, dy;
        var int a, b, diff;
        // make sure x1 <= x2
        if (x1 > x2) {
            let temp = x1;
            let x1 = x2;
            let x2 = temp;
            let temp = y1;
            let y1 = y2;
            let y2 = temp;
        }

        let dx = x2 - x1;
        let dy = y2 - y1;
        // since y axis is pointing down
        // two cases: go top-right (y decreases) or bottom-right (y increases)
        if (dy < 0) {
            let y_incr = -1;
        } else {
            let y_incr = 1;
        }

        let a = 0;
        let b = 0;
        let diff = 0;
        while (~(a > dx) & (((y_incr = 1) & ~(b > dy)) | ((y_incr = -1) & ~(b < dy)))) {
            do Screen.drawPixel(x1+a, y1+b);
            if (diff < 0) {
                let a = a + 1;
                let diff = diff + (dy * y_incr);
            } else {
                let b = b + y_incr;
                let diff = diff - dx;
            }
        }

        return;
    }


    /** Draws a filled rectangle where the top left corner
     *  is (x1, y1) and the bottom right corner is (x2, y2). */
    function void drawRectangle(int x1, int y1, int x2, int y2) {
        while (~(y1 > y2)) {
            do Screen.drawHorizontalLine(x1, x2, y1);
            let y1 = y1 + 1;
        }
        return;
    }

    /** Draws a filled circle of radius r around (cx, cy). */
    function void drawCircle(int cx, int cy, int r) {
        var int dx, dy;
        var int r_squared;

        let dy = -r;
        let r_squared = r * r;
        while (~(dy > r)) {
            let dx = Math.sqrt(r_squared - (dy*dy));
            do Screen.drawHorizontalLine(cx-dx, cx+dx, cy+dy);
            let dy = dy + 1;
        }
        return;
    }
}
